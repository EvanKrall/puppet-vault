# == Class: vault
#
# Installs, configures, and manages vault
#
# === Parameters
#
# [*version*]
#   Specify version of vault binary to download.
#
# [*config_hash*]
#   Use this to populate the JSON config file for vault.
#
# [*install_method*]
#   Defaults to `url` but can be `package` if you want to install via a system
#   package.
#
# [*package_name*]
#   Only valid when the install_method == package. Defaults to `vault`.
#
# [*package_ensure*]
#   Only valid when the install_method == package. Defaults to `latest`.
#
# [*extra_options*]
#   Extra arguments to be passed to the vault agent
#
# [*init_style*]
#   What style of init system your system uses.
#
# [*purge_config_dir*]
#   Purge config files no longer generated by Puppet
class vault (
  $manage_user       = true,
  $user              = 'vault',
  $manage_group      = true,
  $purge_config_dir  = true,
  $group             = 'vault',
  $bin_dir           = '/usr/local/bin',
  $arch              = $vault::params::arch,
  $version           = $vault::params::version,
  $install_method    = $vault::params::install_method,
  $os                = $vault::params::os,
  $download_url      = "https://dl.bintray.com/mitchellh/vault/vault_${version}_${os}_${arch}.zip",
  $package_name      = $vault::params::package_name,
  $package_ensure    = $vault::params::package_ensure,
  $config_dir        = '/etc/vault',
  $extra_options     = '',
  $config_hash       = {},
  $config_defaults   = {},
  $service_enable    = false,
  $service_ensure    = 'stopped',
  $manage_service    = false,
  $init_style        = $vault::params::init_style,
) inherits vault::params {

  validate_bool($purge_config_dir)
  validate_bool($manage_user)
  validate_bool($manage_service)
  validate_hash($config_hash)
  validate_hash($config_defaults)

  $config_hash_real = merge($config_defaults, $config_hash)
  validate_hash($config_hash_real)

  if $config_hash_real['data_dir'] {
    $data_dir = $config_hash_real['data_dir']
  }

  anchor {'vault_first': }
  ->
  class { 'vault::install': } ->
  class { 'vault::config':
    config_hash => $config_hash_real,
    purge       => $purge_config_dir,
  } ~>
  class { 'vault::service': }
  ->
  anchor {'vault_last': }
}
